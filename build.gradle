apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
    maven { url 'http://repository.jboss.org/nexus/content/repositories/deprecated' }
    maven { url 'http://repo.jfrog.org/artifactory/repo' }
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

dependencies {
    def libraryVersions = [
            jersey:                     '2.25.1',
            jackson:                    '2.9.4',
            hibernate:                  '5.2.15.Final',
            hsqldb:                     '2.4.0',
            servlet:                    '4.0.0',
            javaee:                     '7.0',
            jjwt:                       '0.9.0',
            weld:                       '3.0.3.Final',
            annotations:                '13.0',
            assertj:                    '3.9.1',
            junit:                      '4.12',
            tomcat:                     '1.0.1.Final',
            log4j:                      '2.11.0',
            arquillian:                 '1.4.0.Final',
            shrinkWrapResolverGradle:   '3.1.3',
            arquillianRestClient:       '1.0.0.Alpha4'
    ]

    compile "org.jetbrains:annotations:${libraryVersions.annotations}"

    compile "org.hibernate:hibernate-core:${libraryVersions.hibernate}"
    compile "org.hsqldb:hsqldb:${libraryVersions.hsqldb}"

    providedCompile "javax.servlet:javax.servlet-api:${libraryVersions.servlet}"

    //Jersey
    compile "org.glassfish.jersey.core:jersey-server:${libraryVersions.jersey}"
    compile "org.glassfish.jersey.ext.cdi:jersey-cdi1x:${libraryVersions.jersey}"
    compile "org.glassfish.jersey.containers:jersey-container-servlet-core:${libraryVersions.jersey}"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:${libraryVersions.jersey}"
    compile "org.glassfish.jersey.media:jersey-media-multipart:${libraryVersions.jersey}"


    //Jackson
    compile "com.fasterxml.jackson.core:jackson-core:${libraryVersions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${libraryVersions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-databind:${libraryVersions.jackson}"

    //Token
    compile "io.jsonwebtoken:jjwt:${libraryVersions.jjwt}"

    //CDI
    compile "javax:javaee-api:${libraryVersions.javaee}"
    compile "org.jboss.weld.servlet:weld-servlet-core:${libraryVersions.weld}"

    //Logging
    compile "org.apache.logging.log4j:log4j-api:${libraryVersions.log4j}"
    compile "org.apache.logging.log4j:log4j-core:${libraryVersions.log4j}"

    //Websocket
    compile "org.java-websocket:Java-WebSocket:1.3.0"
    providedCompile "javax.websocket:javax.websocket-api:1.1"

    //Testing
    testCompile "junit:junit:${libraryVersions.junit}"
    testCompile "org.jboss.arquillian.junit:arquillian-junit-container:${libraryVersions.arquillian}"
    testCompile "org.jboss.arquillian.protocol:arquillian-protocol-servlet:${libraryVersions.arquillian}"
    testCompile "org.jboss.arquillian.container:arquillian-tomcat-managed-7:${libraryVersions.tomcat}"
    testCompile "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:${libraryVersions.shrinkWrapResolverGradle}"
    testCompile "org.jboss.arquillian.extension:arquillian-rest-client-impl-jersey:${libraryVersions.arquillianRestClient}"
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

gretty {
    // supported values:
    // 'jetty7', 'jetty8', 'jetty9', 'jetty93', 'jetty94', 'tomcat7', 'tomcat8'
    servletContainer = 'tomcat7'
}

task startTestDB {
    doLast{
        javaexec {
            main ="-jar"
            args =  [
                    "./db/db-init.jar",
                    "db/schemas/test2",
                    "test2"
            ]
        }
    }
}